#version 330 core

// Interpolated values from the vertex shaders
in vec2 envUV;
in vec2 UV;
in vec3 surfaceNormal;
in vec3 toLightVector;
in vec3 toCameraVector;
flat in uint texIndex;
flat in uint polyFlag;

// Ouput data
out vec4 color;

// Values that stay constant for the whole mesh.
uniform sampler2DArray textureArray;
uniform sampler2D envMapTextureSampler;
uniform sampler2D carTextureSampler;

uniform vec4 lightColour;
uniform vec3 carColour;
uniform float shineDamper;
uniform float reflectivity;
uniform float envReflectivity;

uniform bool multiTextured;
uniform bool polyFlagged;

void main(){
    vec4 carTexColor = multiTextured ? texture(textureArray, vec3(UV, texIndex)).rgba : texture(carTextureSampler, UV ).rgba;
    vec4 envTexColor = texture( envMapTextureSampler, envUV ).rgba;

    vec3 unitNormal = normalize(surfaceNormal);
    vec3 unitLightVector = normalize(toLightVector);

    // Diffuse
    float nDot1 = dot(unitNormal, unitLightVector);
    float brightness = max(nDot1, 0.2);
    vec3 diffuse = brightness * lightColour.xyz;

    vec3 unitVectorToCamera = normalize(toCameraVector);
    vec3 lightDirection = -unitLightVector;
    vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);

    // Specular
    float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);
    specularFactor = max(specularFactor, 0.0);
    float dampedFactor = pow(specularFactor, shineDamper);
    vec3 finalSpecular = dampedFactor * reflectivity * lightColour.xyz;

	// Output color = color of the texture at the specified UV
	color = vec4(diffuse, 1.0) * (carTexColor + envReflectivity*envTexColor) + vec4(finalSpecular, 1.0);

	// Apply NFS4 Polygon Flags
	if(polyFlagged) {
	    if(((polyFlag << 28) >> 28) == 0xAu){
	        color.a = 0.5f;
	    }
        // NFS3 Remove
	    /*if (polyFlag == 0x1u && carTexColor.a < .1f){
	        color.a = 0.0;
	    }*/
    }
}